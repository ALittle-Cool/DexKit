plugins {
    id "com.android.library"
    id "maven-publish"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "io.luckypray.dexkit"
    compileSdk 32
    ndkVersion = "25.0.8775105"

    defaultConfig {
        minSdk 16
        targetSdk 32


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    cppFlags "-std=c++17",
                            "-funwind-tables",
                            "-fasynchronous-unwind-tables",
                            "-Qunused-arguments",
                            "-fno-rtti",
                            "-fno-exceptions",
                            "-fvisibility=hidden",
                            "-fvisibility-inlines-hidden",
                            "-Wno-unused-value",
                            "-Wno-unused-variable",
                            "-Wno-unused-command-line-argument",
                            "-flto=full",
                            "-ffunction-sections",
                            "-fdata-sections",
                            "-Wl,--gc-sections",
                            "-Oz",
                            "-Wl,--exclude-libs,ALL"
                }
            }
            consumerProguardFiles "proguard-rules.pro"
        }

        debug {
            externalNativeBuild {
                cmake {
                    cppFlags "-std=c++17"
                }
            }
            jniDebuggable true
        }

        buildFeatures {
            prefab true
        }
    }

    externalNativeBuild {
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
            version "3.18.1"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

//    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = "io.luckypray"
                artifactId = "dexkit"
                version = "1.0.0"
            }
        }
    }
}